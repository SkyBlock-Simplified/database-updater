plugins {
    id 'application'
}

group 'dev.sbs'
version '0.1.0'

static def loadEnv() {
    def env = [:]
    try {
        def file = new File('.env')
        def scanner = new Scanner(file)
        while (scanner.hasNextLine()) {
            def line = scanner.nextLine()
            if (line.contains('=')) {
                def pair = line.split('=')
                env.put(pair[0], pair.length == 2 ? pair[1] : '')
            }
        }
    } catch (FileNotFoundException ignored) {
    }
    env.putAll(System.getenv())
    return env
}

def env = loadEnv()

repositories {
    mavenCentral()
    maven {
        name = 'GitHubPackages'
        url = uri('https://maven.pkg.github.com/skyblock-simplified/packages')
        credentials {
            username = env.get('GITHUB_USER')
            password = env.get('GITHUB_PAT')
        }
    }
}

dependencies {
    implementation project(':api')
}

application {
    mainClass = 'dev.sbs.updater.DatabaseUpdater'
}
