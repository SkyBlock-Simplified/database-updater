plugins {
    id 'java'
    id 'application'
}

group 'dev.sbs'
version '0.1.0'

static def loadEnv() {
    def env = [:]
    try {
        def file = new File('.env')
        def scanner = new Scanner(file)
        while (scanner.hasNextLine()) {
            def line = scanner.nextLine()
            if (line.contains('=')) {
                def pair = line.split('=')
                env.put(pair[0], pair.length == 2 ? pair[1] : '')
            }
        }
    } catch (FileNotFoundException ignored) {
    }
    env.putAll(System.getenv())
    return env
}

def env = loadEnv()

repositories {
    mavenCentral()
    maven {
        name = 'GitHubPackages'
        url = uri('https://maven.pkg.github.com/skyblock-simplified/packages')
        credentials {
            username = env.get('GITHUB_USER')
            password = env.get('GITHUB_PAT')
        }
    }
}

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.8'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha5'
    implementation project(':api')

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.8.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.1'
}

application {
    mainClass = 'dev.sbs.datapuller.DataPuller'
}

test {
    useJUnitPlatform()
}
